#! /usr/bin/perl

# Copyright (c) 2020 Marc Espie <espie@openbsd.org>
# 
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
# 
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.


use strict;
use warnings;

use WWW::Mechanize;
use HTML::TreeBuilder;

my $exit = 0;
my $debug = 0;

sub get_tree
{
	my $url = shift;
	my $mech = WWW::Mechanize->new;
	$mech->get($url);
	my $tree = HTML::TreeBuilder->new;
	$tree->parse($mech->content);
	$tree->eof;
	$tree->elementify;
	return $tree;
}

my $badges = {
	'dwarven brewery' => 0,
	'treant guild' => 1,
	'bakers guild' => 2,
	'carpenters guild' => 3,
	'farmers delight' => 4,
	'farmers guild' => 4,
	'blacksmith guild' => 5,
	'luxurious flacon' => 6,
	'golden bracelet' => 7,
	'diamond necklace' => 8,
	'elegant statue' => 9,
	'wand' => 10,
	'magic potion' => 11,
	'witch hat' => 12,
	'wonder society' => 13,
	'elvarian guard' => 14,
	'sack of coins' => 15,
};

sub csv_line
{
	my ($bools, $points) = @_;
	my @line;
	for my $i (0..8) {
		if ($bools->[$i]) {
			push(@line, "=TRUE");
		} else {
			push(@line, "=FALSE");
		}
	}
	for my $j (0..15) {
		push(@line, $points->{$j} // 0);
	}
	return join(',', @line), "\n";
}

# elvengems is somewhat inconsistent in its capitalization
# so we convert everything to lc
sub lc_text
{
	my $o = shift;
	my $t = $o->as_text;
	$t =~ tr/A-Z/a-z/;
	return $t;
}

sub parse_map_row
{
	my ($td, $map, $list) = @_;
	my $header = shift @$td;
	my $h = lc_text($header);

	# weed out score totals and create text array
	my @parsed;

	for my $p (@$td) {
		my $t = lc_text($p);
		if ($t =~ m/.*00$/) {
			next;
		}
		push(@parsed, $t);
	}

	my $points = [];
	# lines are alternating: orange/blue/green, with Qty/Type
	my $color = 0;
	while (@parsed >= 2) {
		my ($qty, $type) = (shift @parsed, shift @parsed);
		# some points DON'T have the same number of badges,
		# that results in blank lines
		if (defined $badges->{$type}) {
			$points->[$color]{$badges->{$type}} = $qty;
		} elsif ($type =~ m/^\s*$/) {
			# that's an empty type
		} else {
			print STDERR "Unknown type in line $h: $type ($qty)\n";
			$exit = 1;
		}
		$color++;
		if ($color == 3) {
			$color = 0;
		}
	}
	# so we want an array with each path tagged with a bool
	my $idx = $map * 3;
	print "$h: " if $debug;
	if ($h eq 'start') {
		# start points have 3* the same color, BUT it's the same point
		my $bools = [];
		$bools->[$idx] = $bools->[$idx+1] = $bools->[$idx+2] = 1;
		push(@{$list->[$idx]}, csv_line($bools, $points->[0]));
	} else {
		for (my $i = 0; $i != 3; $i++) {
			my $bools = [];
			$bools->[$idx+$i] = 1;
			push(@{$list->[$idx+$i]}, 
			    csv_line($bools, $points->[$i]));
		}
	}
}

sub parse_tree
{
	my $tree = shift;

	# extract the 3 maps, numbered
	my $map = 0;
	my $output = [];
	for my $l ($tree->look_down('_tag', 'table', 
	    'class', qr{fa-stage-chart.*})) {
	    	print "------------map #$map\n" if $debug;
		my @tr = $l->look_down('_tag', 'tr');
		my @td = ();
		while (@tr != 0) {
			my $tr = shift @tr;
			print "---", $tr->as_text, "\n" if $debug;
			my @new = $tr->look_down('_tag', 'td');
			if (@new == 0) {
				if (@td == 0) {
					print "skip\n" if $debug;
					next;
				}
				print "parsing\n" if $debug;
				parse_map_row(\@td, $map, $output);
				@td = ();
			} else {
				my $h = lc_text($new[0]);
				if ($h =~ m/^(way\-?point|total|qty\.?)$/) {
					next;
				} 
				push(@td, @new);
			}
		}
		$map++;
	}
	for my $o (@$output) {
		for my $e (@$o) {
			print $e;
		}
	}
}


my $url = shift // "https://elvengems.com/fellowship-adventures/march-2020-adventure/";


my $tree = get_tree($url);
parse_tree($tree);
if ($exit) {
	print STDERR "Fatal: some errors encountered\n";
	exit $exit;
}

